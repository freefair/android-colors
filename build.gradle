buildscript {
    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath 'io.freefair.gradle:maven-plugin:6.5.0.3'
    }
}

plugins {
    id "io.freefair.git-version" version "6.5.0.3"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
    group = 'io.freefair.android-colors'
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'maven-publish'
    apply plugin: "signing"
    apply plugin: "io.freefair.maven-central.validate-poms"

    android {
        compileSdkVersion 31

        defaultConfig {
            versionCode 1
            versionName project.version
            minSdkVersion 1
            targetSdkVersion 31
        }
    }

    publishing {
        publications.withType(MavenPublication) {
            pom {
                url = 'https://github.com/freefair/android-colors'
                name = provider { project.description }
                description = provider { project.description }
                inceptionYear = '2015'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://github.com/freefair/android-colors/blob/master/LICENSE'
                    }
                }
                organization {
                    name = 'FreeFair'
                    url = 'https://github.com/freefair'
                }
                developers {
                    developer {
                        id = 'larsgrefer'
                        name = 'Lars Grefer'
                        email = 'github@larsgrefer.de'
                        timezone = 'Europe/Berlin'
                    }
                }
                ciManagement {
                    system = 'GitHub Actions'
                    url = 'https://github.com/freefair/android-colors/actions'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/freefair/android-colors/issues'
                }
                scm {
                    connection = 'scm:git:https://github.com/freefair/android-colors.git'
                    developerConnection = 'scm:git:git@github.com:freefair/android-colors.git'
                    url = 'https://github.com/freefair/android-colors/'
                }
            }
        }
    }

    signing {
        required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }

        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }

    project.afterEvaluate {
        publishing {
            publications {
                // Creates a Maven publication called "release".
                release(MavenPublication) {
                    // Applies the component for the release build variant.
                    from components.release

                    signing.sign it
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = '7e6204597a774f'
        }
    }
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}